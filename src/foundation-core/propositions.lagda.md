# Propositions

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation-core.propositions where

open import foundation-core.cartesian-product-types using (_√ó_)
open import foundation-core.contractible-types using
  ( is-contr; is-contr-is-equiv; is-contr-equiv'; is-contr-Œ£'; is-prop-is-contr)
open import foundation-core.dependent-pair-types using (Œ£; pair; pr1; pr2)
open import foundation-core.equality-dependent-pair-types using
  ( Eq-Œ£; equiv-eq-pair-Œ£)
open import foundation-core.equivalences using
  ( is-equiv; _‚âÉ_; is-equiv-has-inverse; is-equiv-map-inv-is-equiv)
open import foundation-core.functions using (_‚àò_)
open import foundation-core.homotopies using (refl-htpy)
open import foundation-core.identity-types using
  ( Id; refl; left-inv; inv; _‚àô_; tr)
open import foundation-core.truncated-types using
  ( is-trunc; is-trunc-Id)
open import foundation-core.truncation-levels using (ùïã; neg-two-ùïã; neg-one-ùïã; succ-ùïã)
open import foundation-core.universe-levels using (Level; UU; lsuc; lzero; _‚äî_)
```

## Idea

A type is considered to be a proposition if its identity types are contractible. This condition is equivalent to the condition that it has up to identification at most one element.

## Definition

```agda
is-prop :
  {i : Level} (A : UU i) ‚Üí UU i
is-prop A = is-trunc neg-one-ùïã A

UU-Prop :
  (l : Level) ‚Üí UU (lsuc l)
UU-Prop l = Œ£ (UU l) is-prop

module _
  {l : Level}
  where

  type-Prop : UU-Prop l ‚Üí UU l
  type-Prop P = pr1 P

  abstract
    is-prop-type-Prop : (P : UU-Prop l) ‚Üí is-prop (type-Prop P)
    is-prop-type-Prop P = pr2 P
```

## Examples

We prove here only that any contractible type is a proposition. The fact that the empty type and the unit type are propositions can be found in

```md
foundation.empty-types
foundation.unit-type
```

## Properties

### Equivalent characterizations of propositions

```agda
module _
  {l : Level} (A : UU l)
  where
  
  all-elements-equal : UU l
  all-elements-equal = (x y : A) ‚Üí Id x y
  
  is-proof-irrelevant : UU l
  is-proof-irrelevant = A ‚Üí is-contr A

module _
  {l : Level} {A : UU l}
  where
  
  abstract
    is-prop-all-elements-equal : all-elements-equal A ‚Üí is-prop A
    pr1 (is-prop-all-elements-equal H x y) = (inv (H x x)) ‚àô (H x y)
    pr2 (is-prop-all-elements-equal H x .x) refl = left-inv (H x x)

  abstract
    eq-is-prop' : is-prop A ‚Üí all-elements-equal A
    eq-is-prop' H x y = pr1 (H x y)

  abstract
    eq-is-prop : is-prop A ‚Üí {x y : A} ‚Üí Id x y
    eq-is-prop H {x} {y} = eq-is-prop' H x y

  abstract
    is-proof-irrelevant-all-elements-equal :
      all-elements-equal A ‚Üí is-proof-irrelevant A
    pr1 (is-proof-irrelevant-all-elements-equal H a) = a
    pr2 (is-proof-irrelevant-all-elements-equal H a) = H a

  abstract
    is-proof-irrelevant-is-prop : is-prop A ‚Üí is-proof-irrelevant A
    is-proof-irrelevant-is-prop =
      is-proof-irrelevant-all-elements-equal ‚àò eq-is-prop'

  abstract
    is-prop-is-proof-irrelevant : is-proof-irrelevant A ‚Üí is-prop A
    is-prop-is-proof-irrelevant H x y = is-prop-is-contr (H x) x y

  abstract
    eq-is-proof-irrelevant : is-proof-irrelevant A ‚Üí all-elements-equal A
    eq-is-proof-irrelevant H = eq-is-prop' (is-prop-is-proof-irrelevant H)
```

### A map between propositions is an equivalence if there is a map in the reverse direction

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-equiv-is-prop :
      is-prop A ‚Üí is-prop B ‚Üí {f : A ‚Üí B} ‚Üí (B ‚Üí A) ‚Üí is-equiv f
    is-equiv-is-prop is-prop-A is-prop-B {f} g =
      is-equiv-has-inverse
        ( g)
        ( Œª y ‚Üí eq-is-prop is-prop-B)
        ( Œª x ‚Üí eq-is-prop is-prop-A)

  abstract
    equiv-prop : is-prop A ‚Üí is-prop B ‚Üí (A ‚Üí B) ‚Üí (B ‚Üí A) ‚Üí A ‚âÉ B
    pr1 (equiv-prop is-prop-A is-prop-B f g) = f
    pr2 (equiv-prop is-prop-A is-prop-B f g) =
      is-equiv-is-prop is-prop-A is-prop-B g
```

### Propositions are closed under equivalences

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-prop-is-equiv : {f : A ‚Üí B} ‚Üí is-equiv f ‚Üí is-prop B ‚Üí is-prop A
    is-prop-is-equiv {f} E H =
      is-prop-is-proof-irrelevant
        ( Œª a ‚Üí is-contr-is-equiv B f E (is-proof-irrelevant-is-prop H (f a)))

  abstract
    is-prop-equiv : A ‚âÉ B ‚Üí is-prop B ‚Üí is-prop A
    is-prop-equiv (pair f is-equiv-f) = is-prop-is-equiv is-equiv-f

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-prop-is-equiv' : {f : A ‚Üí B} ‚Üí is-equiv f ‚Üí is-prop A ‚Üí is-prop B
    is-prop-is-equiv' E H =
      is-prop-is-equiv (is-equiv-map-inv-is-equiv E) H

  abstract
    is-prop-equiv' : A ‚âÉ B ‚Üí is-prop A ‚Üí is-prop B
    is-prop-equiv' (pair f is-equiv-f) = is-prop-is-equiv' is-equiv-f
```

### Propositions are closed under dependent pair types

```agda
abstract
  is-prop-Œ£ : {l1 l2 : Level} {A : UU l1} {B : A ‚Üí UU l2} ‚Üí
    is-prop A ‚Üí ((x : A) ‚Üí is-prop (B x)) ‚Üí is-prop (Œ£ A B)
  is-prop-Œ£ H K x y =
    is-contr-equiv'
      ( Eq-Œ£ x y)
      ( equiv-eq-pair-Œ£ x y)
      ( is-contr-Œ£'
        ( H (pr1 x) (pr1 y))
        ( Œª p ‚Üí K (pr1 y) (tr _ p (pr2 x)) (pr2 y)))

Œ£-Prop :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : type-Prop P ‚Üí UU-Prop l2) ‚Üí
  UU-Prop (l1 ‚äî l2)
pr1 (Œ£-Prop P Q) = Œ£ (type-Prop P) (Œª p ‚Üí type-Prop (Q p))
pr2 (Œ£-Prop P Q) =
  is-prop-Œ£
    ( is-prop-type-Prop P)
    ( Œª p ‚Üí is-prop-type-Prop (Q p))
```

### Propositions are closed under cartesian product types

```agda
abstract
  is-prop-prod :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} ‚Üí
    is-prop A ‚Üí is-prop B ‚Üí is-prop (A √ó B)
  is-prop-prod H K = is-prop-Œ£ H (Œª x ‚Üí K)

prod-Prop : {l1 l2 : Level} ‚Üí UU-Prop l1 ‚Üí UU-Prop l2 ‚Üí UU-Prop (l1 ‚äî l2)
pr1 (prod-Prop P Q) = type-Prop P √ó type-Prop Q
pr2 (prod-Prop P Q) = is-prop-prod (is-prop-type-Prop P) (is-prop-type-Prop Q)
```
