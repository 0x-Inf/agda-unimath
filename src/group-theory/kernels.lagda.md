---
title: Kernels
---

```agda
module group-theory.kernels where

open import foundation.equality-cartesian-product-types using (eq-pair)
open import foundation.equality-dependent-pair-types using (eq-pair-Σ)
open import foundation.dependent-pair-types using (_,_; pr1; pr2)
open import foundation.identity-types using (_＝_; ap; _∙_)
open import foundation.injective-maps using (is-emb-is-injective')
open import foundation.propositions using (type-Prop)
open import foundation.sets using (Id-Prop)
open import foundation.universe-levels

open import group-theory.homomorphisms-groups
open import group-theory.embeddings-groups
open import group-theory.subgroups
open import group-theory.groups
```

## Idea

The **kernel** of a group homomorphism `f : A → B` is the subgroup of A consisting of those elements which `f` sends to the unit of B.

## Definition

We define the kernel as the subgroup generated by the predicate which associates to `x` the proposition `f(x) = unit`.

```agda
module _
  {l k : Level} (G : Group l) (H : Group k) (f : type-hom-Group G H)
  where

  subtype-kernel-hom-Group : subset-Group k G
  subtype-kernel-hom-Group x =
    Id-Prop (set-Group H) (map-hom-Group G H f x) (unit-Group H)

  is-in-kernel-hom-Group : type-Group G → UU k
  is-in-kernel-hom-Group x = type-Prop (subtype-kernel-hom-Group x)

  contains-unit-subtype-kernel-hom-Group :
    is-in-kernel-hom-Group (unit-Group G)
  contains-unit-subtype-kernel-hom-Group = preserves-unit-hom-Group G H f

  is-closed-under-mul-subtype-kernel-hom-Group :
    is-closed-under-mul-subset-Group G subtype-kernel-hom-Group
  is-closed-under-mul-subtype-kernel-hom-Group x y p q =
    ( preserves-mul-hom-Group G H f x y) ∙
    ( ( ap (λ (x , y) → mul-Group H x y) (eq-pair p q)) ∙
      ( left-unit-law-Group H _))

  is-closed-under-inv-subtype-kernel-hom-Group :
    is-closed-under-inv-subset-Group G subtype-kernel-hom-Group
  is-closed-under-inv-subtype-kernel-hom-Group x p =
    ( preserves-inverses-hom-Group G H f x) ∙
    ( ap (inv-Group H) p ∙ is-own-inverse-unit-Group H)

  subgroup-kernel-hom-Group : Subgroup k G
  pr1 subgroup-kernel-hom-Group = subtype-kernel-hom-Group
  pr1 (pr2 subgroup-kernel-hom-Group) = contains-unit-subtype-kernel-hom-Group
  pr1 (pr2 (pr2 subgroup-kernel-hom-Group)) =
    is-closed-under-mul-subtype-kernel-hom-Group
  pr2 (pr2 (pr2 subgroup-kernel-hom-Group)) =
    is-closed-under-inv-subtype-kernel-hom-Group

  kernel-hom-Group : Group _
  kernel-hom-Group = group-Subgroup G subgroup-kernel-hom-Group

  inclusion-kernel-hom-Group : type-hom-Group kernel-hom-Group G
  inclusion-kernel-hom-Group =
    inclusion-group-Subgroup G subgroup-kernel-hom-Group

  is-emb-inclusion-kernel-hom-Group :
    is-emb-hom-Group kernel-hom-Group G inclusion-kernel-hom-Group
  is-emb-inclusion-kernel-hom-Group =
    is-emb-inclusion-group-Subgroup G subgroup-kernel-hom-Group

  emb-inclusion-kernel-hom-Group : emb-Group kernel-hom-Group G
  pr1 emb-inclusion-kernel-hom-Group =
    inclusion-kernel-hom-Group
  pr2 emb-inclusion-kernel-hom-Group =
    is-emb-inclusion-kernel-hom-Group
```
