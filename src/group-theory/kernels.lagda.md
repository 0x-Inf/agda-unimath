---
title: Kernels
---

```agda
{-# OPTIONS --without-K --exact-split #-}
module group-theory.kernels where

open import foundation.equality-cartesian-product-types using (eq-pair)
open import foundation.equality-dependent-pair-types using (eq-pair-Σ)
open import foundation.dependent-pair-types using (_,_; pr1; pr2)
open import foundation.identity-types using (_＝_; ap; _∙_)
open import foundation.injective-maps using (is-emb-is-injective')
open import foundation.propositions using (type-Prop)
open import foundation.sets using (Id-Prop)

open import group-theory.homomorphisms-groups
open import group-theory.embeddings-groups
open import group-theory.subgroups
open import group-theory.groups
```

## Idea

The **kernel** of a group homomorphism `f : A → B` is the subgroup of A consisting of those elements which `f` sends to the unit of B.

## Definition

We define the kernel as the subgroup generated by the predicate which associates to `x` the proposition `f(x) = unit`.

```agda
module _ {l k} (G : Group l) (H : Group k) (f : type-hom-Group G H) where
  kernel-hom-Subtype : type-Group G → _
  kernel-hom-Subtype x = Id-Prop (set-Group H) (map-hom-Group G H f x) (unit-Group H)

  contains-unit-kernel-hom-Subtype : type-Prop (kernel-hom-Subtype (unit-Group G))
  contains-unit-kernel-hom-Subtype = preserves-unit-hom-Group G H f

  is-closed-under-mul-kernel-hom-Subtype : is-closed-under-mul-subset-Group G kernel-hom-Subtype
  is-closed-under-mul-kernel-hom-Subtype x y fx=u fy=u =
    preserves-mul-hom-Group G H f x y ∙
    ( ap (λ (x , y) → mul-Group H x y) (eq-pair fx=u fy=u) ∙
      left-unit-law-Group H _)

  is-closed-under-inv-kernel-hom-Subtype : is-closed-under-inv-subset-Group G kernel-hom-Subtype
  is-closed-under-inv-kernel-hom-Subtype x fx=u =
    preserves-inverses-hom-Group G H f x ∙ (ap (inv-Group H) fx=u ∙ is-own-inverse-unit-Group H)

  kernel-hom-Subgroup : Subgroup k G
  kernel-hom-Subgroup = kernel-hom-Subtype , contains-unit-kernel-hom-Subtype , is-closed-under-mul-kernel-hom-Subtype , is-closed-under-inv-kernel-hom-Subtype

  kernel-hom-Group : Group _
  kernel-hom-Group = group-Subgroup G kernel-hom-Subgroup

  inclusion-kernel-hom-Group : emb-Group kernel-hom-Group G
  inclusion-kernel-hom-Group .pr1 = inclusion-group-Subgroup G kernel-hom-Subgroup
  inclusion-kernel-hom-Group .pr2 =
    is-emb-is-injective' (is-set-type-Group kernel-hom-Group) (is-set-type-Group G) _
      (λ x → eq-pair-Σ x (pr1 (is-set-type-Group H _ _ _ _)))
```
